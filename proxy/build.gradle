plugins {
  id 'org.springframework.boot' version '2.3.3.RELEASE'
  id 'com.bmuschko.docker-spring-boot-application' version '5.0.0'
  id 'nebula.override' version '3.0.2'
}

apply plugin: 'io.spring.dependency-management'

repositories {
  mavenCentral()
}

publishing {
  publications {
    nebula(MavenPublication) {
      artifacts {
        // ensure the Spring Boot jar gets published rather than the plain jar
        artifact bootJar
      }
    }
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'io.micrometer:micrometer-registry-prometheus'
  implementation project(':prometheus-rsocket-proxy-server')

  implementation 'io.rsocket:rsocket-micrometer:1.0.2'
  implementation 'io.rsocket:rsocket-transport-netty:1.0.2'

  implementation 'org.xerial.snappy:snappy-java:latest.release'
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
}

if (hasProperty('dockerUser') && hasProperty('dockerPassword')) {
  docker {
    registryCredentials {
      username = dockerUser
      password = dockerPassword
      email = 'tludwig@pivotal.io'
    }

    springBootApplication {
      tag = "$dockerUser/prometheus-rsocket-proxy:${project.version}"
      baseImage = 'openjdk:8'
    }
  }
}
